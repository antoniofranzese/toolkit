#!/bin/zsh

[ -z "$KAFKA_HOME" ] && echo "KAFKA_HOME is not set" && exit 1

ME=$(basename $0)
TOOL=$KAFKA_HOME/bin/kafka-consumer-groups.sh\ --timeout\ ${KAFKA_CLIENT_TIMEOUT:-30000}
CLIP=${$(command -v clip-copy):-cat}

CMD=$1
[[ $# > 0 ]] && shift
CMDLINE="$@"

case $CMD in

	list)
		eval $TOOL $CMDLINE --list
		;;

	browse)
		OUT=$(eval $TOOL $CMDLINE --list \
			| sort \
			| fzf \
				--height=80% \
				--header=$'\n?=Toggle Preview, ^P=Refresh Preview, ^T=Topics\n\nConsumer Groups' \
				--bind='?:toggle-preview' \
				--bind='ctrl-p:refresh-preview' \
				--expect='ctrl-t' \
				--preview-window=hidden:up:70% \
				--preview "$TOOL $CMDLINE --describe --group {}")

		GROUP_KEY=$(head -1 <<< "$OUT")
		GROUP=$(tail -1 <<< "$OUT")
		[ -z $GROUP ] && exit 0

		case $GROUP_KEY in 

			ctrl-t)
				OUT2=$(eval $TOOL $CMDLINE --describe --group $GROUP \
					| fzf \
					    --height=80% \
						--header-lines=2 \
						--header=$'\n^E=Reset to earliest, ^L=Reset to latest, ^O=Reset to offset\n\nTopic offsets for '$GROUP \
						--expect='ctrl-e,ctrl-l,ctrl-o' \
						--preview-window=hidden:up:70% \
						--preview "echo {}" \
					)

				TOPIC_KEY=$(head -1 <<< "$OUT2")
				TOPIC=$( echo $(tail -1  <<< "$OUT2") | awk '{print $2}' )
				[ -z $TOPIC ] && exit 0

				case $TOPIC_KEY in
					ctrl-e)
						echo $TOOL $CMDLINE --group $GROUP --topic $TOPIC --reset-offsets --execute --to-earliest | $CLIP 
						;;

					ctrl-l)
						echo $TOOL $CMDLINE --group $GROUP --topic $TOPIC --reset-offsets --execute --to-latest | $CLIP 
						;;

					ctrl-o)
						echo $TOOL $CMDLINE --group $GROUP --topic $TOPIC --reset-offsets --execute --to-offset \<offset\> | $CLIP 
						;;

					*)
						echo $TOOL $CMDLINE --group $GROUP --topic $TOPIC  | $CLIP 
						;;
				esac
				;;

			*)
				echo $ME $CMDLINE --describe --group $GROUP | $CLIP 
				;;

		esac
		;;

	offsets)
		$ME $CMDLINE --all-groups --describe 2>&1 \
			| grep -v "^$" \
			| grep -v "^GROUP" \
			| grep -v "^Consumer group" \
			| awk '{print $1, $2, $3, $4, $5, $6}' \
			| prepend "GROUP TOPIC PARTITION CURRENT-OFFSET LOG-END-OFFSET LAG" \
			| column -t \
			| fzf --header-lines=1
		;;

	*)
		eval $TOOL $CMD $CMDLINE
		;;

esac
